# VanessaExt - внешняя компонента 1С

Компонента разработана как составная часть инструмента тестирования прикладных решений 
[**Vanessa Automation**](https://github.com/Pr-Mex/vanessa-automation/), хотя имеет 
самостоятельную ценность и может быть использована в составе других продуктов.

Предназначена для Windows и Linux, разработана по технологии Native API.

### Возможности компоненты:
- Получение списка окон и списка процессов
- Управление размерами и положением окна
- Получение снимка окна и снимка экрана
- Обмен данными по протоколу WebSocket
- Эмуляция действий пользователя
- Доступ к данным буфера обмена

### Свойства

- <a href="#ScreenInfo">СвойстваЭкрана (ScreenInfo)</a>
- <a href="#DisplayList">СписокДисплеев (DisplayList)</a>
- <a href="#WindowList">СписокОкон (WindowList)</a>
- <a href="#ProcessId">ИдентификаторПроцесса (ProcessId)</a>
- <a href="#ActiveWindow">АктивноеОкно (ActiveWindow)</a>
- <a href="#ClipboardText">ТекстБуфераОбмена (ClipboardText)</a>
- <a href="#ClipboardImage">КартинкаБуфераОбмена (ClipboardImage)</a>
- <a href="#CursorPos">ПозицияКурсора (CursorPos)</a>

### Методы

Работа с процессами:
- <a href="#FindTestClient">НайтиКлиентТестирования (FindTestClient)</a>
- <a href="#GetProcessList">ПолучитьСписокПроцессов (GetProcessList)</a>
- <a href="#GetProcessInfo">ПолучитьСвойстваПроцесса (GetProcessInfo)</a>
- <a href="#Sleep">Пауза (Sleep)</a>

Информация об окружении:
- <a href="#GetDisplayList">ПолучитьСписокДисплеев (GetDisplayList)</a>
- <a href="#GetDisplayInfo">ПолучитьСвойстваДисплея (GetDisplayInfo)</a>

Управление окном приложения:
- <a href="#GetWindowList">ПолучитьСписокОкон (GetWindowList)</a>
- <a href="#GetWindowInfo">ПолучитьСвойстваОкна (GetWindowInfo)</a>
- <a href="#GetWindowSize">ПолучитьРазмерОкна (GetWindowSize)</a>
- <a href="#SetWindowSize">УстановитьРазмерОкна (SetWindowSize)</a>
- <a href="#SetWindowPos">УстановитьПозициюОкна (SetWindowPos)</a>
- <a href="#ActivateWindow">АктивироватьОкно (ActivateWindow)</a>
- <a href="#MaximixeWindow">РаспахнутьОкно (MaximixeWindow)</a>
- <a href="#RestoreWindow">РазвернутьОкно (RestoreWindow)</a>
- <a href="#MinimizeWindow">СвернутьОкно (MinimizeWindow)</a>

Эмуляция действий пользователя (только для Windows):
- <a href="#EmulateMouse">ЭмуляцияДвиженияМыши (EmulateMouse)</a>
- <a href="#EmulateClick">ЭмуляцияНажатияМыши (EmulateClick)</a>
- <a href="#EmulateDblClick">ЭмуляцияДвойногоНажатия (EmulateDblClick)</a>
- <a href="#EmulateWheel">ЭмуляцияКолесаМыши (EmulateWheel)</a>
- <a href="#EmulateHotkey">ЭмуляцияНажатияКлавиши (EmulateHotkey)</a>
- <a href="#EmulateText">ЭмуляцияВводаТекста (EmulateText)</a>
- <a href="#ShowClick">ПоказатьНажатиеМыши (ShowClick)</a>
- <a href="#VisualizeClick">ВизуализацияНажатияМыши (VisualizeClick)</a>
- <a href="#ShowClickVisualization">ПоказатьВизуализациюНажатияМыши (ShowClickVisualization)</a>
- <a href="#StopClickVisualization">ПрекратитьВизуализациюНажатияМыши (StopClickVisualization)</a>

Обмен данными по протоколу WebSocket (только для Windows):
- <a href="#OpenWebSocket">ОткрытьВебСокет (OpenWebSocket)</a>
- <a href="#SendWebSocket">ПослатьВебСокет (SendWebSocket)</a>
- <a href="#CloseWebSocket">ЗакрытьВебСокет (CloseWebSocket)</a>
- <a href="#WebSocket">ВебСокет (WebSocket)</a>

Захват изображения экрана:
- <a href="#TakeScreenshot">ПолучитьСнимокЭкрана (TakeScreenshot)</a>
- <a href="#CaptureWindow">ПолучитьСнимокОкна (CaptureWindow)</a>
- <a href="#CaptureProcess">ПолучитьСнимокПроцесса (CaptureProcess)</a>

Поиск файлов (только для Windows):
- <a href="#FindFiles">НайтиФайлы (FindFiles)</a>

Работа с буфером обмена:
- <a href="#EmptyClipboard">ОчиститьБуферОбмена (EmptyClipboard)</a>

Расширенный функционал:
- работа с буфером обмена <a href="#ClipboardControl">**ClipboardControl**</a>.
- запуск приложений <a href="#ProcessControl">**ProcessControl**</a>.

### Общая информация

Внешняя компонента поддерживает как синхронный, так и асинхронный вызов.
Для асинхронного вызова в полном соответствии с документацией Синтакс-помощника
1С:Предприятие применяются методы:
- НачатьВызов<ИмяМетода>(<ОписаниеОповещения>, <Параметры>)
- НачатьПолучение<ИмяСвойства>(<ОписаниеОповещения>)

Пример асинхронного вызова внешней компоненты:
```bsl
&НаКлиенте
Процедура ПодключениеВнешнейКомпонентыЗавершение(Подключение, ДополнительныеПараметры) Экспорт
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолученаВерсияКомпоненты", ЭтотОбъект);
	ВнешняяКомпонента.НачатьПолучениеВерсия(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПолученаВерсияКомпоненты(Значение, ДополнительныеПараметры) Экспорт
	Заголовок = "Управление окнами, версия " + Значение;
КонецПроцедуры	
```

Далее по тексту все примеры будут приводиться для синхронных вызовов. В публикуемом примере
[**VanessaExt.epf**](https://github.com/lintest/VanessaExt/releases) используются только асинхронные вызовы.

Многие свойства и методы компоненты возвращают сложные типы данных, которые сериализованы 
в строку формата JSON. Поэтому имеет смысл объявить в вызывающем модуле универсальную 
функцию, которая будет использоваться ниже в примерах работы компоненты:
```bsl
Функция ПрочитатьСтрокуJSON(ТекстJSON)
	Если ПустаяСтрока(ТекстJSON) Тогда
		Возврат Неопределено;
	Иначе
		ПоляДаты = Новый Массив;
		ПоляДаты.Добавить("date");
		ПоляДаты.Добавить("CreationDate");
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(ТекстJSON);
		Возврат ПрочитатьJSON(ЧтениеJSON, , ПоляДаты);
	КонецЕсли;
КонецФункции
```
### Сборка проекта

Готовая сборка внешней компоненты находится в файле 
[/Example/Templates/VanessaExt/Ext/Template.bin](https://github.com/lintest/VanessaExt/raw/master/Example/Templates/VanessaExt/Ext/Template.bin)

Порядок самостоятельной сборки внешней компоненты из исходников:
1. Для сборки компоненты необходимо установить Visual Studio Community 2019
2. Скачиваем и устанавливаем библиотеку [**boost**](http://www.boost.org/users/download/)
3. Чтобы работала сборка примера обработки EPF надо установить OneScript версии 1.0.20 или выше
4. Устанавливаем VirtualBox и разворачиваем в минимальной конфигурации Ubuntu 18.04 или CentOS 8
5. Устанавливаем на Linux необходимые пакеты (см. ниже) и дополнения гостевой ОС
6. Подключаем в VirtualBox общую папку с исходными текстами внешней компоненты
7. В среде Linux для компиляции библиотек запустить ./build.sh
8. В среде Window для завершения сборки запустить ./compile.bat

Сборка для Linux в CentOS 8:
```bash
yum -y group install "Development Tools"
yum -y install cmake glibc-devel.i686 glibc-devel libuuid-devel
yum -y install libstdc++-devel.i686 gtk2-devel.i686 glib2-devel.i686
yum -y install libstdc++-devel.x86_64 gtk2-devel.x86_64 glib2-devel.x86_64
yum -y install libxtst-devel.i686 libxtst-devel.x86_64
git clone https://github.com/lintest/VanessaExt.git
cd VanessaExt
./build.sh
```

Сборка для Linux в Ubuntu 18.04:
```bash
sudo dpkg --add-architecture i386
sudo apt update
sudo apt install -y build-essential cmake git
sudo apt install -y gcc-multilib g++-multilib
sudo apt install -y uuid-dev libx11-dev libxrandr-dev libpng-dev
sudo apt install -y libxtst-dev libxtst-dev:i386
git clone https://github.com/lintest/VanessaExt.git
cd VanessaExt
./build.sh
```

Сборка библотеки [**boost**](https://www.boost.org/doc/libs/1_72_0/more/getting_started/windows.html#prepare-to-use-a-boost-library-binary) для Windows
```Batchfile
b2.exe toolset=msvc link=static threading=multi runtime-link=static release stage
```

Сборка библотеки [**boost**] для Linux
```Batchfile
./b2 cxxflags=-fPIC link=static threading=multi runtime-link=static release stage
```

Установка на VirtualBox дополнений гостевой ОС для Linux:
```bash
mkdir -p /media/cdrom
mount -r /dev/cdrom /media/cdrom
cd /media/cdrom
./VBoxLinuxAdditions.run
sudo usermod -a -G vboxsf "$USER"
reboot
```

Чтобы работала сборка демонстрационной внешней обработки под Linux:
+ Установить [onescript](https://oscript.io/);
+ Добавить в PATH путь к актуальной версии платформы 1с.
  Например, так:
  ```bash
  sudo ln -s /opt/1cv8/x86_64/8.3.18.891/1cv8 /usr/local/bin/1cv8
  ```
  
Для отладки компоненты под Linux:
+ Закомментировать строку _strip -s bin/libVanessaExt*.so_ в файле **build.sh**
+ В файле **CMakeLists.txt** заменить строку:

    _SET(CMAKE_BUILD_TYPE Release CACHE STRING "Build configurations" FORCE)_

  на:
  
    _SET(CMAKE_BUILD_TYPE **Debug** CACHE STRING "Build configurations" FORCE)_
+ При необходимости удалить установленную ранее библиотеку в папке **~/.1cv8/1C/1cv8/ExtCompT/**

Если в момент присоединения к процессу 1с возникает ошибка доступа,
можно выполнить следующую [инструкцию](https://askubuntu.com/questions/41629/after-upgrade-gdb-wont-attach-to-process).
***

## Установка и подключение

Для создания объекта экземпляра внешней компоненты используйте имя **WindowsControl**.
В прилагаемом примере файлы внешней компоненты хранятся в макете **VanessaExt**, реквизит формы 
**МестоположениеКомпоненты** используется для передачи макета компоненты между сервером и клиентом.
Для установки и подключения внешней компоненты рекомендуется использовать следующий программный код:

```bsl
&НаКлиенте
Перем ИдентификаторКомпоненты, ВнешняяКомпонента;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МакетКомпоненты = РеквизитФормыВЗначение("Объект").ПолучитьМакет("VanessaExt");
	МестоположениеКомпоненты = ПоместитьВоВременноеХранилище(МакетКомпоненты, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИдентификаторКомпоненты = "_" + СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
	ВыполнитьПодключениеВнешнейКомпоненты(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодключениеВнешнейКомпоненты(ДополнительныеПараметры) Экспорт
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодключениеВнешнейКомпонентыЗавершение", ЭтаФорма, ДополнительныеПараметры);
	НачатьПодключениеВнешнейКомпоненты(ОписаниеОповещения, МестоположениеКомпоненты, ИдентификаторКомпоненты, ТипВнешнейКомпоненты.Native); 
КонецПроцедуры	

&НаКлиенте
Процедура ПодключениеВнешнейКомпонентыЗавершение(Подключение, ДополнительныеПараметры) Экспорт
	Если Подключение Тогда
		ВнешняяКомпонента = Новый("AddIn." + ИдентификаторКомпоненты + ".WindowsControl");
	ИначеЕсли ДополнительныеПараметры = Истина Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПодключениеВнешнейКомпоненты", ЭтаФорма, Ложь);
		НачатьУстановкуВнешнейКомпоненты(ОписаниеОповещения, МестоположениеКомпоненты);
	КонецЕсли;
КонецПроцедуры	

```

## Свойства

### <a name="ScreenInfo">СвойстваЭкрана / ScreenInfo</a>
Тип значения: Строка (только чтение)
- Возвращает строку с текстом в формате JSON, при чтении которого получаем
объект типа ***Структура*** с размерами экрана и рабочей области.

### <a name="DisplayList">СписокДисплеев / DisplayList</a>
Тип значения: Строка (только чтение)
- Возвращает строку с текстом в формате JSON, при чтении которого получаем
объект типа ***Массив*** из элементов типа ***Структура*** с размерами дисплеев мониторов.

### <a name="WindowList">СписокОкон / WindowList</a>
Тип значения: Строка (только чтение)
- Возвращает строку с текстом в формате JSON, при чтении которого получаем
объект типа ***Массив*** из элементов типа ***Структура*** с информацией об окнах 
верхнего уровня: дескриптор окна, диентификатор процесса, владелец, заголовок окна.

### <a name="ProcessId">ИдентификаторПроцесса / ProcessId</a>
Тип значения: Целое число (только чтение)
- Возвращает идентификатор основного процесса приложения 1С, в сеансе которого 
вызывается внешняя компонента.

### <a name="ActiveWindow">АктивноеОкно / ActiveWindow</a>
Тип значения: Целое число (чтение и запись)
- Дескриптор приоритетного окна (окна, с которым пользователь в настоящее время работает). 

### <a name="ClipboardText">ТекстБуфераОбмена / ClipboardText</a>
Тип значения: Строка (чтение и запись)
- Предоставляет доступ к содержимому буфера обмена в текстовом формате. 

### <a name="ClipboardImage">КартинкаБуфераОбмена / ClipboardImage</a>
Тип значения: Двоичные данные (чтение и запись)
- Предоставляет доступ к содержимому буфера обмена в формате картинки PNG. 

```bsl
ПотокВПамяти = Новый ПотокВПамяти;
БиблиотекаКартинок.ДиалогИнформация.Записать(ПотокВПамяти);
ДвоичныеДанные = ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
ВнешняяКомпонента.КартинкаБуфераОбмена = ДвоичныеДанные;
```

## Методы
### <a name="FindTestClient">НайтиКлиентТестирования(НомерПорта) / FindTestClient</a>
Возвращает текст в формате JSON, при чтении которого получаем объект типа ***Структура***, 
сотдержащий информацию о клиенте тестирования, найденному по номеру порта, 
который присутствует в командной строке экземпляра клиента тестирования.

Параметры функции:
- **НомерПорта** (обязательный), Тип: Целое число

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем
объект типа ***Структура*** с подробной информацией о найденном процессе.
    - ProcessId - идентификатор процесса (Число)
	- CommandLine - командная строка процесса (Строка)
	- CreationDate - дата старта процесса (Дата)
	- Window - дескриптор основного окна (Число)
	- Title - заголовок основного окна (Строка)

```bsl
ТекстJSON = ВнешняяКомпонента.НайтиКлиентТестирования(ПортПодключения);
СтруктураСвойствПроцесса = ПрочитатьСтрокуJSON(ТекстJSON);
Если СтруктураСвойствПроцесса <> Неопределено Тогда
	ИдентификаторПроцесса = СтруктураСвойствПроцесса.ProcessId;
	ДескрипторОкна = СтруктураСвойствПроцесса.Window;
КонецЕсли;
```
### <a name="GetProcessList">ПолучитьСписокПроцессов(ИспользоватьОтбор) / GetProcessList</a>
Получает список запущенных процессов с возможностью отбора только процессов 1С.
Отбор производится по наименованию исполняемого файла на соответствие маске "1cv8*".

Параметры функции:
- **ИспользоватьОтбор** (обязательный), Тип: Булево

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем
типа ***Массив*** из элементов типа ***Структура*** информацией о процессах.

```bsl
ТекстJSON = ВнешняяКомпонента.ПолучитьСписокПроцессов(Истина);
Для каждого Стр из ПрочитатьСтрокуJSON(ТекстJSON) Цикл
	НоваяСтр = СписокПроцессов.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
КонецЦикла;
```

### <a name="GetProcessInfo">ПолучитьСвойстваПроцесса(ИдентификаторПроцесса) / GetProcessInfo</a>
По идентификатору процесса возвращает всю доступную информацию о процессе.

Параметры функции:
- **ИдентификаторПроцесса** (обязательный), Тип: Целое число

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем
объект типа ***Структура*** с подробной информацией о процессе:
    - ProcessId - идентификатор процесса (Число)
	- CommandLine - командная строка процесса (Строка)
	- CreationDate - дата старта процесса (Дата)

```bsl
ТекстJSON = ВнешняяКомпонента.ПолучитьСвойстваПроцесса(ИдентификаторПроцесса);
СтруктураСвойстваПроцесса = ПрочитатьСтрокуJSON(ТекстJSON);
```

### <a name="WebSocket">ВебСокет(Адрес, Команда) / WebSocket</a>
Простейшая функция однократного обмена данными по протоколу WebSocket.
Может использоваться для интеграции с браузером Google Chrome.

Параметры функции:
- **Адрес** (обязательный), Тип: Строка
- **Команда** (обязательный), Тип: Строка

Тип возвращаемого значения: Строка
- Содержит данные, полученные как ответ сервера WebSocket на отправленную команду.

```bsl
	АдресВебСокет = "ws://localhost:9222/devtools/page/771BF9AF6EE6F712337EF74397960652";
	КомандаJSON = "{""id"":1,""method"":""Page.captureScreenshot"",""params"":{""format"":""png""}";
	ТекстJSON = ВнешняяКомпонента.ВебСокет(АдресВебСокет, КомандаJSON);
	ДанныеJSON = ПрочитатьСтрокуJSON(ТекстJSON);
	ДвоичныеДанные = Base64Значение(ДанныеJSON.result.data);
```
Подробная информация о Chrome DevTools Protocol доступна по адресу:
* https://chromedevtools.github.io/devtools-protocol/

### <a name="OpenWebSocket">ОткрытьВебСокет(Адрес) / OpenWebSocket</a>
Подключение к серверу WebSocket для начала многократного обмена данными.
Может использоваться для интеграции с браузером Google Chrome.

Параметры функции:
- **Адрес** (обязательный), Тип: Строка

Тип возвращаемого значения: Строка
- Пустая строка в случае успеха, либо сообщение об ошибке.

```bsl
АдресВебСокет = "ws://localhost:9222/devtools/page/771BF9AF6EE6F712337EF74397960652";
Результат  = ВнешняяКомпонента.ОткрытьВебСокет(АдресВебСокет);
Если Не ПустаяСтрока(Результат) Тогда
	Сообщить(Результат);
КонецЕсли
```

### <a name="SendWebSocket">ПослатьВебСокет(Команда) / SendWebSocket</a>
Обмен данными по протоколу WebSocket: отправка пакета данных и получение результата.
Может использоваться для интеграции с браузером Google Chrome.

Параметры функции:
- **Команда** (обязательный), Тип: Строка

Тип возвращаемого значения: Строка
- Содержит данные, полученные как ответ сервера WebSocket на отправленную команду.

```bsl
КомандаJSON = "{""id"":1,""method"":""Page.captureScreenshot""}";
ТекстJSON = ВнешняяКомпонента.ПослатьВебСокет(КомандаJSON);
ДанныеJSON = ПрочитатьСтрокуJSON(ТекстJSON);
ДвоичныеДанные = Base64Значение(ДанныеJSON.result.data);
```

### <a name="CloseWebSocket">ЗакрытьВебСокет() / CloseWebSocket</a>
Закрытие соединения WebSocket.

Возвращаемое значение отсутствует.

```bsl
ВнешняяКомпонента.ЗакрытьВебСокет();
```

### <a name="Sleep">Пауза(Милисекунды) / Sleep</a>
Приостанавливает выполнение программного кода на заданное количество миллисекунд.

```bsl
ВнешняяКомпонента.Пауза(1000);
```

### <a name="GetDisplayList">ПолучитьСписокДисплеев(ДескрипторОкна) / GetDisplayList</a>
По дескриптору окна получает список дисплеев, на которых располагается окно или его части.

Параметры функции:
- **ДескрипторОкна** (необязательный), Тип: Целое число
Если параметр не задан, будет получена информация обо всех дисплеях.

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем объект 
типа ***Массив*** из элементов типа ***Структура*** со свойствами дисплея: координаты 
границ, высота и ширина, наименование дисплея, координаты и размер рабочей области дисплея.

```bsl
ТекстJSON = ВнешняяКомпонента.ПолучитьСписокДисплеев(ДескрипторОкна);
Для каждого ЭлементМассива из ПрочитатьСтрокуJSON(ТекстJSON) Цикл
	ЗаполнитьЗначенияСвойств(СписокДисплеев.Добавить(), ЭлементМассива);
КонецЦикла;
```

### <a name="GetDisplayInfo">ПолучитьСвойстваДисплея(ДескрипторОкна) / GetDisplayInfo</a>
По дескриптору окна получает свойства дисплея, на котором располагается наибольшая часть окна.

Параметры функции:
- **ДескрипторОкна** (необязательный), Тип: Целое число
Если параметр не задан, будет получена информация для активного окна.

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем объект 
типа ***Структура*** со свойствами дисплея: координаты границ, высота и ширина,
наименование дисплея, координаты и размер рабочей области дисплея.

```bsl
ТекстJSON = ВнешняяКомпонента.ПолучитьСвойстваДисплея(ДескрипторОкна);
СвойстваДисплея = ПрочитатьСтрокуJSON(ТекстJSON);
ВнешняяКомпонента.УстановитьПозициюОкна(ДескрипторОкна, СвойстваДисплея.Left, СвойстваДисплея.Top);
```

### <a name="GetWindowList">ПолучитьСписокОкон(ИдентификаторПроцесса) / GetWindowList</a>
Возвращает текст в формате JSON, при чтении которого получаем объект типа ***Массив***
из элементов типа ***Структура*** с информацией об окнах, принадлежащих указанному процессу.

Параметры функции:
- **ИдентификаторПроцесса** (обязательный), Тип: Целое число
	- Если параметр нулевой или не задан, возвращается список всех окон.

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем объект 
типа ***Массив*** из элементов типа ***Структура*** с информацией о найденых окнах:
	- Window - дескриптор окна (Число)
    - ProcessId - идентификатор процесса (Число)
	- Class - идентификатор класса окна (Строка)
	- Title - заголовок окна (Строка)
	- Owner - окно владелец (Число)

```bsl
ТекстJSON = ВнешняяКомпонента.ПолучитьСписокОкон(ИдентификаторПроцесса);
МассивОкон = ПрочитатьСтрокуJSON(ТекстJSON);
ТаблицаОкон.Очистить();
Для каждого Стр из МассивОкон Цикл
	ЗаполнитьЗначенияСвойств(ТаблицаОкон.Добавить(), Стр);
КонецЦикла;
```

### <a name="GetWindowInfo">ПолучитьСвойстваОкна(ДескрипторОкна) / GetWindowInfo</a>
Возвращает текст в формате JSON, при чтении которого получаем объект 
типа ***Структура*** с информацией об основных свойставх окна:

Параметры функции:
- **ДескрипторОкна** (обязательный), Тип: Целое число
	- Если параметр нулевой, будут получены свойства активного окна.

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем объект 
типа ***Структура*** с информацией об основных свойствах окна:
	- Window - дескриптор окна (Число)
    - ProcessId - идентификатор процесса (Число)
	- Maximized - распахнуто, максимизировано (Булево)
	- Class - идентификатор класса окна (Строка)
	- Title - заголовок окна (Строка)
	- Owner - окно владелец (Число)

```bsl
ТекстJSON = ВнешняяКомпонента.ПолучитьСписокОкон(ИдентификаторПроцесса);
СвойстваОкна = ПрочитатьСтрокуJSON(ТекстJSON);
ЗаголовокОкна = СвойстваОкна.Title;
```

### <a name="GetWindowSize">ПолучитьРазмерОкна(ДескрипторОкна) / GetWindowSize</a>
Возвращает текст в формате JSON, при чтении которого получаем объект объект 
типа ***Структура*** с информацией о размерах и позиции окна.

Параметры функции:
- **ДескрипторОкна** (обязательный), Тип: Целое число
	- Если параметр нулевой, будут получены размеры активного окна.

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем объект 
типа ***Структура*** с информацией о размерах и позиции окна:
    - Left - левая граница (Число)
	- Top - верхняя граница (Число)
	- Right - правая граница (Строка)
	- Bottom - нижняя граница (Строка)
	- Width - ширина окна (Число)
	- Height - высота высота (Число)
	- Window - дескриптор окна (Число)

### <a name="ActivateWindow">АктивироватьОкно(ДескрипторОкна) / ActivateWindow</a>
Активирует окно по дескриптору.

Параметры функции:
- **ДескрипторОкна** (обязательный), Тип: Целое число

```bsl
ВнешняяКомпонента.АктивироватьОкно(ДескрипторОкна);
```
### <a name="MaximixeWindow">РаспахнутьОкно(ДескрипторОкна) / MaximixeWindow</a>
Распахиват (максимизирует) окно, разворачивая его на всё рабочую область экрана.

Параметры функции:
- **ДескрипторОкна** (обязательный), Тип: Целое число

```bsl
ВнешняяКомпонента.РаспахнутьОкно(ДескрипторОкна);
```
### <a name="RestoreWindow">РазвернутьОкно(ДескрипторОкна) / RestoreWindow</a>
Показывает окно в нормальном режиме отображения, если оно было свёрнуто или распахнуто.

Параметры функции:
- **ДескрипторОкна** (обязательный), Тип: Целое число

```bsl
ВнешняяКомпонента.РазвернутьОкно(ДескрипторОкна);
```

### <a name="SetCursorPos">УстановитьПозициюКурсора(X , Y) / SetCursorPos</a>
Перемещает позицию курсора (мышки) в указанную точку экрана.

```bsl
ВнешняяКомпонента.УстановитьПозициюКурсора(X, Y);
```

### <a name="EmulateMouse">ЭмуляцияДвиженияМыши(X , Y, КоличествоШагов, ТаймаутШага) / EmulateMouse</a>
Плавно перемещает позицию курсора (мышки) в указанную точку экрана за обозначенное 
количество шагов, на каждом шаге производися задержка на указанное количество милисекунд.

```bsl
ВнешняяКомпонента.ЭмуляцияДвиженияМыши(X, Y, 1000, 3);
```

### <a name="EmulateClick">ЭмуляцияНажатияМыши(КнопкаМыши) / EmulateClick</a>
Эмулирует нажаите указанной кнопки мыши.

Параметры процедуры:
- **КнопкаМыши** (обязательный), Тип: Целое число
    - 0 - Левая кнопка
    - 1 - Правая кнопка
    - 2 - Средняя кнопка

```bsl
ВнешняяКомпонента.ЭмуляцияНажатияМыши(0);
```

### <a name="EmulateDblClick">ЭмуляцияДвойногоНажатия() / EmulateDblClick</a>
Эмулирует нажаите левой кнопкой мыши.

```bsl
ВнешняяКомпонента.ЭмуляцияДвойногоНажатия();
```

### <a name="EmulateWheel">ЭмуляцияКолесаМыши(Направление) / EmulateWheel</a>
Эмулирует вращение колеса мыши.

Параметры процедуры:
- **КнопкаМыши** (обязательный), Тип: Целое число
    - (+1) - Вращение вперёд
    - (-1) - Вращение назад

```bsl
ВнешняяКомпонента.ЭмуляцияКолесаМыши(1);
```

### <a name="EmulateHotkey">ЭмуляцияНажатияКлавиши(Клавиша, Флаги) / EmulateHotkey</a>
Эмулирует нажаите пользователем горячей клавиши. Параметры функции 
совместимы с типовым объектом **СочетаниеКлавиш**. Для вызова
рекомендуется использовать следующий программный код.

```bsl
СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.F11, Ложь, Истина, Ложь);

ЗаписьXML = Новый ЗаписьXML;
ЗаписьXML.УстановитьСтроку();
ЗаписатьXML(ЗаписьXML, СочетаниеКлавиш);
ТекстXML = ЗаписьXML.Закрыть();
ЧтениеXML = Новый ЧтениеXML; 
ЧтениеXML.УстановитьСтроку(ТекстXML);
ПостроительDOM = Новый ПостроительDOM;
ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
КорневойУзел = ДокументDOM.ПервыйДочерний;
Для каждого ДочернийУзел из КорневойУзел.ДочерниеУзлы Цикл
	Если ДочернийУзел.ИмяУзла = "vKey" Тогда
		Клавиша = Число(ДочернийУзел.ТекстовоеСодержимое);
	ИначеЕсли ДочернийУзел.ИмяУзла = "flags" Тогда
		Флаги = Число(ДочернийУзел.ТекстовоеСодержимое);
	КонецЕсли;
КонецЦикла;

ВнешняяКомпонента.ЭмуляцияНажатияКлавиши(Клавиша, Флаги);
```

Альтернативный вариант вызова функции, в первом параметра передаётся строка
в формате JSON, представляющая собой массив с кодами виртуальных клавиш.
Второй параметр при этом необязателен и игнорируется.
```bsl
ТекстJSON = "[91, 69]"; // Win+E
ВнешняяКомпонента.ЭмуляцияНажатияКлавиши(ТекстJSON);
```

### <a name="EmulateText">ЭмуляцияВводаТекста(Текст, Таймаут) / EmulateText</a>
Эмулирует последовательный ввод пользователем текста.

```bsl
ВнешняяКомпонента.ЭмуляцияВводаТекста("Текст коментария", 100);
```

### <a name="ShowClick">ПоказатьНажатиеМыши(Цвет, Радиус, Толщина, Задержка, Прозрачность) / ShowClick</a>
Однократный показ визуального эффекта в виде расходящихся концентрических кругов, для демонстрации нажатия мыши пользователем.

Параметры процедуры:
- **Цвет** (необязательный), Тип: Целое число
- **Радиус** (необязательный), Тип: Целое число
- **Толщина** (необязательный), Тип: Целое число
- **Задержка** (необязательный), Тип: Целое число
- **Прозрачность** (необязательный), Тип: Целое число

```bsl
ВнешняяКомпонента.ПоказатьНажатиеМыши(255, 30, 12, 12, 127);
```

### <a name="VisualizeClick">ВизуализацияНажатияМыши(Цвет, Радиус, Толщина, Задержка, Прозрачность) / VisualizeClick</a>
Включение режима, при котором каждое нажатие мыши сопровождается визуальным эффектом в виде расходящихся концентрических кругов.

Параметры процедуры:
- **Цвет** (необязательный), Тип: Целое число
- **Радиус** (необязательный), Тип: Целое число
- **Толщина** (необязательный), Тип: Целое число
- **Задержка** (необязательный), Тип: Целое число
- **Прозрачность** (необязательный), Тип: Целое число

```bsl
ВнешняяКомпонента.ВизуализацияНажатияМыши(255, 30, 12, 12, 127);
```

### <a name="ShowClickVisualization">ПоказатьВизуализациюНажатияМыши() / ShowClickVisualization</a>
Однократный показ визуального эффекта в виде расходящихся концентрических кругов ко текущим координатам 
указателя мыши с текущими настройками, если включен режим визуализации.

```bsl
ВнешняяКомпонента.ПоказатьВизуализациюНажатияМыши();
```

### <a name="StopClickVisualization">ПрекратитьВизуализациюНажатияМыши() / StopClickVisualization</a>
Отключение режима, при котором каждое нажатие мыши сопровождается визуальным эффектом.

```bsl
ВнешняяКомпонента.ПрекратитьВизуализациюНажатияМыши();
```

### <a name="TakeScreenshot">ПолучитьСнимокЭкрана(Режим) / TakeScreenshot</a>
Получает снимок экрана или активного окна, в зависимости от переданного параметра.

Параметры функции:
- **Режим** (обязательный), Тип: Целое число
    - 0 - Чтобы сделать снимок всего экрана
    - 1 - Чтобы сделать снимок области активного окна

Тип возвращаемого значения: Двоичные данные
- Возвращает картинку в формате PNG.

```bsl
ДвоичныеДанные = ВнешняяКомпонента.ПолучитьСнимокЭкрана(0);
```

### <a name="CaptureWindow">ПолучитьСнимокОкна(ДескрипторОкна) / CaptureWindow</a>
Получает снимок произвольного окна по его дескриптору.

Параметры функции:
- **ДескрипторОкна** (обязательный), Тип: Целое число
    - 0 - Чтобы сделать снимок активного окна

Тип возвращаемого значения: Двоичные данные
- Возвращает картинку в формате PNG.

```bsl
ДвоичныеДанные = ВнешняяКомпонента.ПолучитьСнимокОкна(ДескрипторОкна);
```

### <a name="CaptureProcess">ПолучитьСнимокПроцесса(ИдентификаторПроцесса) / CaptureProcess</a>
Получает снимок верхнего окна экземпляра 1С:Предприятие по идентификатору процесса.

Параметры функции:
- **ИдентификаторПроцесса** (обязательный), Тип: Целое число
    - Идентификатор процесса приложения 1С:Предприятие

Тип возвращаемого значения: Двоичные данные
- Возвращает картинку в формате PNG.

```bsl
ДвоичныеДанные = ВнешняяКомпонента.ПолучитьСнимокПроцесса(ИдентификаторПроцесса);
```

### <a name="FindFiles">НайтиФайлы(Директория, МаскаПоиска, ИскомыйТекст, ИгнорироватьРегистр) / FindFiles</a>
Рекурсивный поиск файлов по маске и содержимому.

Параметры функции:
- **Директория** (обязательный), Тип: Строка
    - Начальная директория для поиска файлов
- **МаскаПоиска** (обязательный), Тип: Строка
    - Маска поиска файлов, например: *.txt
- **ИскомыйТекст** (обязательный), Тип: Строка
    - Искомый тект для поиска по содержимому файла
- **ИгнорироватьРегистр** (необязательный), Тип: Строка
    - Проверка на соответствие регистра искомого текста

Тип возвращаемого значения: Строка
- Содержит строку с текстом в формате JSON, при чтении которого получаем объект 
типа ***Массив*** из элементов типа ***Структура*** с информацией о найденых файлах:
	- name - имя файла (Строка)
	- path - полный путь файла (Строка)
    - size - размер файла в байтах (Число)
    - date - дата модификации файла (Дата)

```bsl
ТекстJSON = ВнешняяКомпонента.НайтиФайлы(Директория, МаскаПоиска, ИскомыйТекст, ИгнорироватьРегистр);
Данные = ПрочитатьСтрокуJSON(ТекстJSON);
СписокФайлов.Очистить();
Если ТипЗнч(Данные) = Тип("Массив") Тогда
	Для каждого Стр Из Данные Цикл
		НоваяСтр = СписокФайлов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
	КонецЦикла;
КонецЕсли;

```

### <a name="EmptyClipboard">ОчиститьБуферОбмена() / EmptyClipboard</a>
Очищает буфер обмена.

```bsl
ВнешняяКомпонента.ОчиститьБуферОбмена();
```

## <a name="ClipboardControl">Работа с буфером обмена: ClipboardControl</a>

Внешняя компонента содержит также отдельный класс для работы с буфером обмена.
```bsl
&НаКлиенте
Перем БуферОбмена;

&НаКлиенте
Процедура ПодключениеВнешнейКомпонентыЗавершение(Подключение, ДополнительныеПараметры) Экспорт
	БуферОбмена = Новый("AddIn." + ИдентификаторКомпоненты + ".ClipboardControl");
КонецПроцедуры	

```
### Свойства
- Текст / Text</a>
- Картинка / Image</a>
- Файлы / Files</a>
- Формат / Format</a>
- Версия / Version</a>

### Методы
- ЗаписатьТекст / SetText
- ЗаписатьФайлы / SetFiles
- ЗаписатьКартинку / SetImage
- Очистить / Empty

## <a name="ProcessControl">Запуск прилложений: ProcessControl</a>

Внешняя компонента содержит также отдельный класс для запуска приложений.

### Свойства
- ИдентификаторПроцесса / ProcessId</a>
- Активный / IsActive</a>
- КодВозврата / ExitCode</a>

### Методы
- Создать / Create
- Ждать / Wait
- Прервать / Terminate
- ВвестиДанные / InputData

```bsl
&НаКлиенте
Перем ИдентификаторКомпоненты, ПроцессЗаписи;

&НаКлиенте
Процедура НачатьЗапись(Команда)
	КоманднаяСтрока = "ffmpeg -f gdigrab -framerate 30 -i desktop output.mp4";
	ПроцессЗаписи = Новый("AddIn." + ИдентификаторКомпоненты + ".ProcessControl");
	ПроцессЗаписи.Создать(КоманднаяСтрока, ОтображатьОкноКонвертора);
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьЗапись(Команда)
	ПроцессЗаписи.ВвестиДанные("q");
КонецПроцедуры
```

***

При разработке использовались библиотеки:
- [cpp-c11-make-screenshot by Roman Shuvalov](https://github.com/Butataki/cpp-x11-make-screenshot)
- [Clip Library by David Capello](https://github.com/dacap/clip)
- [Boost C++ Libraries](https://www.boost.org/)
